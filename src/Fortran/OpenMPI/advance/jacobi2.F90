program jacobi2
   use mpi
   implicit none
   !定义数组的规模
   integer, parameter :: totalsize = 16
   integer, parameter :: mysize = totalsize/4
   integer, parameter :: steps = 100
   integer n, myid, numprocs, i, j, rc
   real a(totalsize, mysize + 2), b(totalsize, mysize + 2)

   integer begin_col, end_col, ierr
   integer status(MPI_STATUS_SIZE)

   call mpi_init(ierr)
   call mpi_comm_rank(MPI_COMM_WORLD, myid, ierr)
   call mpi_comm_size(MPI_COMM_WORLD, numprocs, ierr)

   !数组初始化
   do j = 1, mysize + 2
      do i = 1, totalsize
         a(i, j) = 0.0
      end do
   end do

   if (myid == 0) then
      do i = 1, totalsize
         a(i, 2) = 8.0
      end do
   end if

   if (myid == 3) then
      do i = 1, totalsize
         a(i, mysize + 1) = 8.0
      end do
   end if

   do i = 1, mysize + 2
      a(1, i) = 8.0
      a(totalsize, i) = 8.0
   end do

   !jacobi 迭代部分
   do n = 1, steps
      !从左向右平移数据
      if (myid == 0) then
         call MPI_SEND(a(1, mysize + 1), totalsize, MPI_REAL, myid + 1, 10, MPI_COMM_WORLD, ierr)
      else if (myid == 3) then
         call MPI_RECV(a(1, 1), totalsize, MPI_REAL, myid - 1, 10, MPI_COMM_WORLD, status, ierr)
      else
         call MPI_SENDRECV(a(1, mysize + 1), totalsize, MPI_REAL, myid + 1, &
                           10, a(1, 1), totalsize, MPI_REAL, myid - 1, 10, MPI_COMM_WORLD, status, ierr)
      end if
      !从右向左平移数据
      if (myid == 0) then
         call MPI_RECV(a(1, mysize + 2), totalsize, MPI_REAL, myid + 1, 10, MPI_COMM_WORLD, status, ierr)
      else if (myid == 3) then
         call MPI_SEND(a(1, 2), totalsize, MPI_REAL, myid - 1, 10, MPI_COMM_WORLD, ierr)
      else
         call MPI_SENDRECV(a(1, 2), totalsize, MPI_REAL, myid - 1, &
                           10, a(1, mysize + 2), totalsize, MPI_REAL, myid + 1, 10, MPI_COMM_WORLD, status, ierr)
      end if

      begin_col = 2
      end_col = mysize + 1

      if (myid == 0) then
         begin_col = 3
      end if

      if (myid == 3) then
         end_col = mysize
      end if

      do j = begin_col, end_col
         do i = 2, totalsize - 1
            b(i, j) = (a(i, j + 1) + a(i, j - 1) + a(i + 1, j) + a(i - 1, j))*0.25
         end do
      end do

      do j = begin_col, end_col
         do i = 2, totalsize - 1
            a(i, j) = b(i, j)
         end do
      end do

   end do

   do i = 2, totalsize - 1
      print *, myid, (a(i, j), j=begin_col, end_col)
   end do

   call mpi_finalize(rc)
end program
